buildscript {
    apply from: 'dependencies.gradle'

    repositories {
		mavenCentral()
	}
    dependencies {
	    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0"
	}
}

plugins {
	id 'maven-publish'
}

allprojects {
	group "org.golemites"
	version "0.1.0-SNAPSHOT"
}

subprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'java-library'

	compileJava {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		options.compilerArgs += '-parameters'
	}

	if (new File("$project.projectDir/bnd.bnd").exists()) {
		apply plugin: 'biz.aQute.bnd.builder'
	}

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
		maven { url "http://repo.maven.apache.org/maven2" }
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allJava
		archiveClassifier = 'sources'
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact sourcesJar
			}
		}
		repositories {
			mavenLocal()
		}
	}
}


